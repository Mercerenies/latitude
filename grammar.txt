
<line> ::= <stmt> "."
<stmt> ::= <simplechain> <stdname> <rhs> |
           <chain> <opname> <rhs1> |
           <literalish>
<rhs> ::= <lambda> |
          <shortarglist> |
          ":=" <stmt> |
          "::=" <stmt> |
          ":" <arglist> |
          "=" <stmt> |
          <shortarglist> "=" <stmt> |
          "<-" <stmt>
<rhs1> ::= <verysimplechain> |
           <shortarglist> |
           ":=" <stmt> |
           "::=" <stmt> |
           ":" <arglist> |
           "=" <stmt> |
           <shortarglist> "=" <stmt> |
           "<-" <stmt>
<arglist> ::= <lambda> |
              <arg> <arglist1>
<arglist1> ::= <lambda> |
               "," <arg> <arglist1>
<arg> ::= <chain> <stdname> |
          <chain> <opname> <verysimplechain> |
          <simplechain> <stdname> <shortarglist> |
          <chain> <opname> <shortarglist> |
          <literalish>
<chain> ::= <chain> <opname> <verysimplechain> |
            <simplechain>
<simplechain> ::= <simplechain> <stdname> |
                  <simplechain> <stdname> <shortarglist> |
                  <literalish> |
                  <lambda>
<verysimplechain> ::= <verysimplechainl> <stdname> |
                      <verysimplechainl> <stdname> <shortarglist> |
<verysimplechainl> ::= <verysimplechain> |
                       <lambda>
<shortarglist> ::= "(" <arglist> ")" |
                   "(" <chain> <name> ":" <arg> ")" |
                   <literal>
<literalish> ::= "~" <text> <literalish> |
                 "(" <stmt> ")" |
                 <literal>
<literal> ::= "{" <linelist> "}" |
              <number> |
              "\"" <text> "\"" |
              "'" <text> |
              "'" "(" <text> ")" |
              "~" <text> |
              "[" <arglist> "]" |
              "'" "[" <literallist> "]" |
              "#" "\"" <text> "\"" "#" |
              "#" "(" <text> ")" |
              [ "-" | "+" ] "0" <letter> { <alphanum> } |
              "@" "{" <linelist> "}" |
              "@" "(" <number> "," <number> ")" |
              "#'" <text> |
              "#'" <literalish>
<linelist> ::= <line> <linelist> |
               <lambda>
<literallist> ::= <listlit> <literallist1> |
                  <lambda>
<literallist1> ::= <lambda> |
                   "," <listlit> <literallist1>
<listlit> ::= "\"" <text> "\"" |
              "'" <text> |
              "~" <text> |
              <number> |
              "'" "(" <text> ")" |
              "[" <literallist> "]" |
              "#" "\"" <text> "\"" "#" |
              <name>
