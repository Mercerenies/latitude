
<line> ::= <stmt> "."
<stmt> ::= <chain> <name> <rhs> |
           <literalish>
<rhs> ::= <lambda> |
          <shortarglist> |
          ":=" <stmt> |
          "::=" <stmt> |
          ":" <arglist> |
          "=" <stmt> |
          <shortarglist> "=" <stmt> |
          "<-" <stmt>
<arglist> ::= <lambda> |
              <arg> <arglist1>
<arglist1> ::= <lambda> |
               "," <arg> <arglist1>
<arg> ::= <nonemptychain>
<nonemptychain> ::= <chain> <name> |
                    <chain> <name> <shortarglist> |
                    <literalish>
<chain> ::= <nonemptychain> |
            <lambda>
<shortarglist> ::= "(" <arglist> ")" |
                   "(" <chain> <name> ":" <arg> ")" |
                   <literal>
<literalish> ::= "~" <text> <literalish> |
                 "(" <stmt> ")" |
                 <literal>
<literal> ::= "{" <linelist> "}" |
              <number> |
              "\"" <text> "\"" |
              "'" <text> |
              "'" "(" <text> ")" |
              "~" <text> |
              "[" <arglist> "]" |
              "'" "[" <literallist> "]" |
              "#" "\"" <text> "\"" "#" |
              "#" "(" <text> ")" |
              "0" <letter> { <alphanum> } |
              "@" "{" <linelist> "}" |
              "@" "(" <number> "," <number> ")" |
              "#'" <text>
<linelist> ::= <line> <linelist> |
               <lambda>
<literallist> ::= <listlit> <literallist1> |
                  <lambda>
<literallist1> ::= <lambda> |
                   "," <listlit> <literallist1>
<listlit> ::= "\"" <text> "\"" |
              "'" <text> |
              "~" <text> |
              <number> |
              "'" "(" <text> ")" |
              "[" <literallist> "]" |
              <name>
