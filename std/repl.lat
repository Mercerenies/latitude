; This is NOT imported through latitude.lat; it is imported later during the spawning
; of the REPL object itself

Kernel := Kernel.
stderr := stderr.

REPL := Object clone.

REPL quitter := { jump := Nil.
                  setJump := { parent jump := $1. }.
                  callCC { inner := $1.
                           callCC { setJump: $1.
                                    inner call: meta Nil. }.
                           Kernel kill. }.
                  jump. }.
REPL quit := { self quitter call: meta Nil. }.
REPL exception := meta Nil.
REPL lastResult := meta Nil.

global quit := { self REPL quit. }.
global $except := { self REPL exception. }.
global $it := { self REPL lastResult. }.

REPL read := { stdout puts: "> ".
               stdin readln. }.
REPL eval := { Kernel eval: global, global, $1. }.
REPL print := { stdout println: $1. }.
REPL loop := { localize.
               loop { { expr := this read.
                        this lastResult := this eval: expr.
                        this print: this lastResult. } resolve: { meta True. },
                                                                { this exception := $1.
                                                                  stderr putln: "***** EXCEPTION *****".
                                                                  stderr putln: this exception pretty.
                                                                  this exception printStackTrace. }. }. }.

global REPL := REPL.
