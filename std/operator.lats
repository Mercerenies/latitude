;;;; Copyright (c) 2018 Silvio Mayolo
;;;; See LICENSE.txt for licensing details

;; Operator type
global Operator := Object clone.
Operator prec := 30.
Operator assoc := 'left.
Operator toString := {
  curr := self.
  if (curr === Operator) then {
    "Operator".
  } else {
    "operator (" ++ curr prec ++ ", " ++ curr assoc ++ ")".
  }.
}.
global operator := {
  curr := Operator clone.
  curr prec := $1.
  curr assoc := $2.
  curr.
}.

global Associativity ::= Enumeration of '[Left, Right, None].
Associativity Left  inner := 'left.
Associativity Right inner := 'right.
Associativity None  inner := 'none.

meta operators get '=~  = operator ( 5, Associativity None ).
meta operators get '==  = operator ( 5, Associativity None ).
meta operators get '=== = operator ( 5, Associativity None ).
meta operators get '<   = operator ( 5, Associativity None ).
meta operators get '<=  = operator ( 5, Associativity None ).
meta operators get '>   = operator ( 5, Associativity None ).
meta operators get '>=  = operator ( 5, Associativity None ).
meta operators get '/=  = operator ( 5, Associativity None ).
meta operators get '++  = operator (10, Associativity Left ).
meta operators get '<>  = operator (15, Associativity Left ).
meta operators get '<|  = operator (20, Associativity Right).
meta operators get '|>  = operator (25, Associativity Left ).
;; 30 is the default for new operators
meta operators get '+   = operator (35, Associativity Left ).
meta operators get '-   = operator (35, Associativity Left ).
meta operators get '/   = operator (40, Associativity Left ).
meta operators get '*   = operator (45, Associativity Left ).
meta operators get '^   = operator (50, Associativity Right).


;; Versioning

global Version ::= Object clone.

Version Release := Enumeration of '[Dev, Alpha, Beta, Full].

Version major := meta sys latitudeVersion#: 0.
Version minor := meta sys latitudeVersion#: 1.
Version build := meta sys latitudeVersion#: 2.
Version release := Version Release value (meta sys latitudeVersion#: 3).

Version == := {
  (self major == $1 major) &&
    (self minor == $1 minor) &&
    (self build == $1 build) &&
    (self release == $1 release).
}.

Version < := {
  localize.
  takes '[other].
  cond {
    when (this major < other major) do { True. }.
    when (this major > other major) do { False. }.
    when (this minor < other minor) do { True. }.
    when (this minor > other minor) do { False. }.
    when (this build < other build) do { True. }.
    when (this build > other build) do { False. }.
    else { False. }. ; We could possibly compare the release types to one another
  }.
}.
