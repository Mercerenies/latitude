;;* MODULE test/dict
;;* PACKAGE latitude

;;;; Copyright (c) 2018 Silvio Mayolo
;;;; See LICENSE.txt for licensing details

use 'unit-test importAll.

dict := $whereAmI.
TestModule inject: dict.

dict addTest 'dict-to-string do {
  eq: [=>] toString, "[=>]".
  eq: [ 'a => 1 ] toString, "['a => 1]".
}.

dict addTest 'dict-cloning do {

  a := [ 'foo => 1, 'bar => 2 ].
  b := a clone.
  c := a dup.

  a get 'foo = "a".
  b get 'bar = "b".
  b get 'baz = "c".

  eq: a, [ 'foo => "a", 'bar => 2 ].
  eq: b, [ 'foo => 1, 'bar => "b", 'baz => "c" ].
  eq: c, [ 'foo => 1, 'bar => 2 ].

}.

dict addTest 'dict-random-access do {

  a := [ 'foo => 1 ].

  truthy { a has? 'foo. }.
  truthy { a has? 'bar not. }.

  eq: a get 'foo, 1.
  throws (err TypeError) do { a get 'bar. }. ;; Should be SlotError :(

  a get 'foo = 2.
  eq: a get 'foo, 2.

  a get 'bar = 3.
  truthy { a has? 'bar. }.
  eq: a get 'bar, 3.

}.

dict addTest 'dict-key-deletion do {

  a := [ 'foo => 1, 'bar => 2 ].

  a delete: 'baz.
  truthy { a has? 'foo. }.
  truthy { a has? 'bar. }.

  a delete: 'bar.
  truthy { a has? 'foo. }.
  truthy { a has? 'bar not. }.

}.

dict addTest 'dict-iteration do {

  truthy { [=>] iterator end?. }.

  arr := [ 'a => 1 ] to (Array).
  eq: arr length, 1.
  eq: arr nth (0) car, 'a.
  eq: arr nth (0) cdr, 1.

}.

dict addTest 'dict-keys-and-values do {

  eq: [=>] keys to (Array), [].
  eq: [=>] values to (Array), [].

  eq: [ 'a => 1 ] keys to (Array), ['a].
  eq: [ 'a => 1 ] values to (Array), [1].

}.

dict addTest 'dict-equality do {

  eq: [ 'a => 1 ], [ 'a => 1 ].

  dict := [ 'a => 1 ].
  dict get 'b = 2.
  eq: dict, [ 'a => 1, 'b => 2 ].

}.

dict.
