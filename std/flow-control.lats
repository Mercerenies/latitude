
;; Continuations
Cont call := { meta sys exitCC#: self, $1. }.
Cont toString := "Cont".
global callCC := {
  meta sys callCC#: Cont clone, { parent dynamic $1. }.
}.

;; Booleans
Object toBool := True.
False toBool := False.
Nil toBool := False.
Object true? := False.
True true? := True.
Object false? := False.
False false? := True.
Object nil? := False.
Nil nil? := True.
Boolean toString := "Boolean".
True toString := "True".
False toString := "False".
Nil toString := "Nil".
Object falsify := {
  hold 'self toBool := False.
  hold 'self.
}.

;; Control Statements
; TODO Make `self` for the conditionals well-defined
global if := {
  truthy := $1 toBool.
  Object clone tap {
    self then := {
      trueCase := { parent dynamic $1. }.
      Object clone tap {
        self else := {
          falseCase := { parent dynamic $1. }.
          meta sys ifThenElse#: True, truthy, { trueCase. }, { falseCase. }.
        }.
      }.
    }.
  }.
}.
Object ifTrue := {
  if (self) then { parent dynamic $1. } else { Nil. }.
  hold 'self.
}.
Object ifFalse := {
  if (self) then { Nil. } else { parent dynamic $1. }.
  hold 'self.
}.
Object and := {
  if (self) then { parent dynamic $1. } else { False. }.
}.
Object or := {
  fst := self.
  if (fst) then { fst. } else { parent dynamic $1. }.
}.
Object not := {
  if (self) then { False. } else { True. }.
}.

;; Loops
global loop := {
  restart := callCC { $1. }.
  $1.
  restart call: restart.
}.
global while := {
  cond := { parent dynamic $1. }.
  Object clone tap {
    self do := {
      stmt := { parent dynamic $1. }.
      callCC {
        $break := { parent dynamic $1 call. }.
        loop {
          if (cond)
            then { stmt. }
            else { $break: Nil. }.
        }.
      }.
    }.
  }.
}.

Number times := {
  localize.
  block := { parent dynamic $1. }.
  i := 0.
  incr := {
    parent i := i + 1.
  }.
  while { (i) < (this). } do {
    block (i).
    incr.
  }.
}.
Number upto := {
  localize.
  takes '[n].
  Object clone tap {
    self do := {
      block := { parent dynamic $1. }.
      i := this.
      incr := { parent i := i + 1. }.
      while { (i) < (n). } do {
        block (i).
        incr.
      }.
    }.
  }.
}.
Number downto := {
  localize.
  takes '[n].
  Object clone tap {
    self do := {
      block := { parent dynamic $1. }.
      i := this.
      decr := {
        parent i := i - 1.
      }.
      while { (i) > (n). } do {
        block (i).
        decr.
      }.
    }.
  }.
}.

;; Cond Statement
global cond := {
  takes '[block].
  callCC {
    finished := $1.
    whenProc := proc {
      if ($1)
        then {
          Object clone tap {
            self do := { finished call: $1. }.
          }.
        } else {
          Object clone tap {
            self do := { }.
          }.
        }.
    }.
    procd := dynamic invokeSpecial:
      hold 'block,
      {
        $1 when := { whenProc call. }.
        $1 else := { self when (True) do { parent dynamic $1. }. }.
      }.
    procd call.
    Nil.
  }.
}.

;; Return the script
here.
